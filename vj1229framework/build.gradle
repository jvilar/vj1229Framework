apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
group = 'com.github.jvilar'

android {
    compileSdk 34

    namespace = "es.uji.vj1229.framework"

    defaultConfig {
        minSdkVersion 21
        versionCode 5
        versionName "v2024.rc1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

   /* publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }*/
}

// Allows using ./gradlew generateRelesaseJavadoc to generate the javadocs
// in the javadoc directory. Does not integrate with jitpack.
android.libraryVariants.all { variant ->
    if (variant.name == "release") {
        //task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        task("javadoc", type: Javadoc) {
            description "Generates Javadoc for $variant.name."

            source = variant.javaCompile.source
            destinationDir = file("$rootDir/javadoc/")
            failOnError false

            doFirst {
                ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
                classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)

                options.addStringOption "-show-members", "package"
            }
        }

        task javadocJar(type: Jar, dependsOn: javadoc) {
            archiveClassifier = 'javadoc'
            from javadoc.destinationDir
        }
    }
}

publishing {
    publications {
        // Creates a Maven publication called "release".
        release(MavenPublication) {
            // You can then customize attributes of the publication as shown below.
            groupId = 'es.uji.vj1229'
            artifactId = 'framework'
            version = 'v2024.rc1'

            // Applies the component for the release build variant.
            afterEvaluate {
                from components.release
                artifact javadocJar
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.6.1'
    testImplementation 'androidx.test:core:1.5.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
