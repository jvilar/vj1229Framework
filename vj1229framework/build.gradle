apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
group = 'com.github.jvilar'

android {
    compileSdk 34

    namespace = "es.uji.vj1229.framework"

    defaultConfig {
        minSdkVersion 21
        versionCode 5
        versionName "v2024.rc1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = 'sources'
}


task javadoc(type: Javadoc) {
    failOnError false
    options.addStringOption('author', '-quiet')
    source = android.sourceSets.main.java.sourceFiles
    afterEvaluate {
        classpath += project.files(project.android.getBootClasspath().join(File.pathSeparator))
    }
}



task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        // Creates a Maven publication called "release".
        release(MavenPublication) {
            // You can then customize attributes of the publication as shown below.
            groupId = 'es.uji.vj1229.framework'
            artifactId = 'final'
            version = 'v2024.rc1'

            // Applies the component for the release build variant.
            afterEvaluate {
                from components.release
                artifact javadocJar
                artifact sourcesJar
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.6.1'
    testImplementation 'androidx.test:core:1.5.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
